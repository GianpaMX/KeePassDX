apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-parcelize'
apply plugin: 'kotlin-kapt'
apply plugin: 'maven-publish'

android {
    compileSdkVersion 32
    buildToolsVersion "32.0.0"
    ndkVersion "21.4.7075529"

    namespace = 'com.kunzisoft.keepass.database'

    defaultConfig {
        minSdkVersion 15
        targetSdk 32

        aarMetadata {
            minCompileSdk = 31
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"

        kapt {
            arguments {
                arg("room.incremental", "true")
                arg("room.schemaLocation", "$projectDir/schemas".toString())
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    publishing {
        singleVariant('release') {
            withSourcesJar()
        }
    }
}

def room_version = "2.4.3"

dependencies {
    implementation "androidx.core:core-ktx:$android_core_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.4'
    // Color
    implementation 'com.github.Kunzisoft:AndroidClearChroma:2.6'
    // Time
    implementation 'joda-time:joda-time:2.10.13'
    // Apache Commons
    implementation 'commons-io:commons-io:2.8.0'
    implementation 'commons-codec:commons-codec:1.15'

    // Database
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    implementation project(path: ':crypto')

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
}

publishing {
    publications {
        release(MavenPublication) {
            groupId = 'com.github.GianpaMX.KeePassDX'
            artifactId = 'database'
            version = System.getenv("VERSION") ?: '0.0.1'

            afterEvaluate {
                from components.release
            }
        }
    }
}
